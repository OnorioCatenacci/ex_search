---- -*- Mode: rpl; -*-                                                                            
---- vim:syn=rosie
----
---- elixir.rpl     Rosie Pattern Language patterns the Elixir programming language
----
---- AUTHORS: Onorio Catenacci

rpl 1.2

package elixir

-- comment = "#"
comment_char = "#"
comment_line = (^ ~ comment_char .* $)
-- test comment_line accepts "# This is a comment"
-- test comment_line accepts "#### This is also a comment"
-- test comment_line rejects "defmodule Test do #an end of line comment"

module_name = {~ [:upper:]{1}{[:alnum:]/"."}+ ~}
-- test module_name accepts " Test "
-- test module_name accepts "Test"
-- test module_name accepts " Test.Submodule "
-- test module_name accepts "Test.Submodule"
-- test module_name accepts " Test.Submodule.Submodule "
-- test module_name accepts "Test.Submodule.Submodule"
-- test module_name accepts " DtwRunnerWeb.ChannelCase "
-- test module_name accepts "DtwRunnerWeb.ChannelCase"
-- test module_name rejects " test "
-- test module_name rejects "test"
-- test module_name rejects " test.Submodule "
-- test module_name rejects "test.Submodule"

grammar 
   module_start = ^[:space:]* "defmodule"  module_name  "do" [:space:]*$
in 
   module = ~ !comment_char module_start
end

-- test module accepts "defmodule Test do   "
-- test module accepts "defmodule Test do"
-- test module accepts "defmodule DtwRunnerWeb.ChannelCase do"
-- test module accepts "defmodule       Test      do   "
-- test module rejects "defmodule test do   "
-- test module rejects "#defmodule Test do"
-- test module rejects "# defmodule Test do"

alias function_name = (~ {[[:lower:] [_]]}+ ~)
-- test function_name accepts "my_function"
-- test function_name accepts "myfunction"
-- test function_name rejects "Myfunction"
-- test function_name rejects "My_function"
-- test function_name rejects "my.function"

local alias parameter = ([_]{0,1}[:alnum:])+

local alias parameter_list = {"(" ({parameter} ","{0,1})* ")"}
-- test local parameter_list accepts "(a, b)"
-- test local parameter_list accepts "()"
-- test local parameter_list accepts "(a)"
-- test local parameter_list accepts "(a, b, c)"
-- test local parameter_list accepts "(_a, b, c)"
-- test local parameter_list rejects "a,b"
-- test local parameter_list rejects "("
-- test local parameter_list rejects " (a, b)"

alias function = ^!comment_char "def"[p]{0,1} ~ function_name ![:space:] parameter_list ~ "do" ~$
-- test function accepts "def test_func() do"
-- test function accepts "def test_func(a) do"
-- test function accepts "def test_func(a,b) do"
-- test function accepts "def testfunc() do"
-- test function rejects "def BadName() do"
-- test function rejects "def bad.name() do"
-- test function rejects "# def test_func() do"
-- test function rejects "# defp test_func() do"
-- test function rejects "#def test_func() do"
-- test function rejects "#defp test_func() do"

