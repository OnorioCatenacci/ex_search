---- -*- Mode: rpl; -*-                                                                            
---- vim:syn=rosie
----
---- elixir.rpl     Rosie Pattern Language patterns the Elixir programming language
----
---- AUTHORS: Onorio Catenacci

rpl 1.2

package elixir

-- comment = "#"
comment = "\u0023"
comment_line = {comment ~ .* $}
-- test comment_line accepts "# This is a comment"
-- test comment_line rejects "defmodule Test do #an end of line comment"

local alias module_name = {[:upper:]{1}{[:alnum:]/"."}+}
-- test local module_name accepts "Test"
-- test local module_name accepts "Test.Submodule"
-- test local module_name accepts "Test.Submodule.Submodule"
-- test local module_name rejects "test"
-- test local module_name rejects "test.Submodule"

alias module = {"defmodule" ~ module_name ~ "do" ~}
-- test module accepts "defmodule Test do   "
-- test module accepts "defmodule Test do"
-- test module accepts "defmodule       Test      do   "
-- test module rejects "defmodule test do   "

local alias function_name = {[[:lower:] [_]]}+
-- test local function_name accepts "my_function"
-- test local function_name accepts "myfunction"
-- test local function_name rejects "Myfunction"
-- test local function_name rejects "My_function"

alias function = {"def"[p]{0,1} ~ function_name ~ "do"}
-- test function accepts "def test_func do"
-- test function accepts "def testfunc do"
-- test function rejects "def BadName do"
-- test function rejects "def bad.name do"
